Version 1
SubGoalCombiner SGC_AND
// https://mod.io/g/baldursgate3/r/onetimereward-triggers-with-osiris
INITSECTION
DB_OneTimeRewards_SimpleSortingBags(1,(ITEMROOT)SimpleSortingBagsMaster_6b1ff3a4-b547-4e0a-b5cf-a0fb8e128333);
DB_OneTimeRewards_InjuryKits(1,(ITEMROOT)TOOL_Hospital_Surgery_Kit_B_55fb2f34-00d7-4149-8b52-e189e05ba0ca);
KBSECTION
// On game start, add stuff to inventory
IF
LevelGameplayStarted(_,_)
AND
DB_Avatars(_Player)
THEN
PROC_OneTimeReward_GiveAllRewards(_Player);

// Wrapper PROC to give both rewards
PROC
PROC_OneTimeReward_GiveAllRewards((GUIDSTRING)_Player)
THEN
PROC_OneTimeReward_GiveReward_SimpleSortingBags(1,1,1,_Player);
PROC_OneTimeReward_GiveReward_InjuryKits(1,2,1,_Player);

// Give Simple Sorting Bags
PROC
PROC_OneTimeReward_GiveReward_SimpleSortingBags((INTEGER)_RewardNo,(INTEGER)_Amount,(INTEGER)_Notification,(GUIDSTRING)_Player)
AND
NOT DB_OneTimeRewards_RewardGiven_SimpleSortingBags(_RewardNo)
AND
DB_OneTimeRewards_SimpleSortingBags(_RewardNo,_Reward)
THEN
DB_OneTimeRewards_RewardGiven_SimpleSortingBags(_RewardNo);
TemplateAddTo(_Reward,_Player,_Amount,_Notification);

// Give Injury Kits
PROC
PROC_OneTimeReward_GiveReward_InjuryKits((INTEGER)_RewardNo,(INTEGER)_Amount,(INTEGER)_Notification,(GUIDSTRING)_Player)
AND
NOT DB_OneTimeRewards_RewardGiven_InjuryKits(_RewardNo)
AND
DB_OneTimeRewards_InjuryKits(_RewardNo,_Reward)
THEN
DB_OneTimeRewards_RewardGiven_InjuryKits(_RewardNo);
TemplateAddTo(_Reward,_Player,_Amount,_Notification);

EXITSECTION

ENDEXITSECTION